import eei::*;
import corectrl::*;

module memunit (
    clk   : input   clock                                    ,
    rst   : input   reset                                    ,
    valid : input   logic                                    ,
    is_new: input   logic                                    , /// whether the instruction is newly supplied
    ctrl  : input   InstCtrl                                 , /// InstCtrl of instruction
    addr  : input   Addr                                     , /// address to be accessed
    rs2   : input   UIntX                                    , /// data to be written by store instruction
    rdata : output  UIntX                                    , /// result of load instruction (valid when stall = 0)
    stall : output  logic                                    , /// memory access instruction not completed
    membus: modport membus_if::<MEM_DATA_WIDTH, XLEN>::master, /// interface with memory
) {
    /// Determine if an instruction accesses memory
    function inst_is_memop (
        ctrl: input InstCtrl,
    ) -> logic {
        return ctrl.itype == InstType::S || ctrl.is_load;
    }

    /// Determine if an instruction is a store instruction
    function inst_is_store (
        ctrl: input InstCtrl,
    ) -> logic {
        return inst_is_memop(ctrl) && !ctrl.is_load;
    }

    /// enum state of memunit
    enum State: logic<2> {
        Init, /// state that accepts instructions
        WaitReady, /// state that waits for memory operation to become available
        WaitValid, /// waits for memory operation to finish
    }

    var state: State;

    var req_wen  : logic                    ;
    var req_addr : Addr                     ;
    var req_wdata: logic<MEM_DATA_WIDTH>    ;
    var req_wmask: logic<MEM_DATA_WIDTH / 8>;

    const W   : u32                   = XLEN; // XLEN alias
    let D   : logic<MEM_DATA_WIDTH> = membus.rdata; // rdata alias
    let sext: logic                 = ctrl.funct3[2] == 1'b0; // Whether to perform sign extension or not?

    always_comb {
        // Memory access
        membus.valid = state == State::WaitReady;
        membus.addr  = req_addr;
        membus.wen   = req_wen;
        membus.wdata = req_wdata;
        membus.wmask = req_wmask;

        // load result
        rdata = case ctrl.funct3[1:0] {
            // load byte => (signed/unsigned) extension 16bits + 8bits
            2'b00  : case addr[1:0] {
                0      : {sext & D[7] repeat W - 8, D[7:0]},
                1      : {sext & D[15] repeat W - 8, D[15:8]},
                2      : {sext & D[23] repeat W - 8, D[23:16]},
                3      : {sext & D[31] repeat W - 8, D[31:24]},
                default: 'x,
            },
            // load half => (signed/unsigned) extension 8bits + 16bits
            2'b01  : case addr[1:0] {
                0      : {sext & D[15] repeat W - 16, D[15:0]},
                2      : {sext & D[31] repeat W - 16, D[31:16]},
                default: 'x,
            },
            2'b10  : D, // LW
            default: 'x,
        };

        // judge stall
        stall = valid & case state {
            State::Init     : is_new && inst_is_memop(ctrl),
            State::WaitReady: 1,
            State::WaitValid: !membus.rvalid,
            default         : 0,
        };
    }

    always_ff {
        if_reset {
            state     = State::Init;
            req_wen   = 0;
            req_addr  = 0;
            req_wdata = 0;
            req_wmask = 0;
        } else {
            if valid {
                case state {
                    State::Init: if is_new & inst_is_memop(ctrl) {
                        state     = State::WaitReady;
                        req_wen   = inst_is_store(ctrl);
                        req_addr  = addr;
                        req_wdata = rs2 << {addr[1:0], 3'b0};
                        req_wmask = case ctrl.funct3[1:0] {
                            2'b00  : 4'b1 << addr[1:0], // In case of SB instruction, shift 1 left by the lower 2 bits of the address
                            2'b01  : case addr[1:0] { // For SH instruction
                                2      : 4'b1100, // Write to upper 2 bytes
                                0      : 4'b0011, // Write in the lower 2 bytes
                                default: 'x,
                            },
                            2'b10  : 4'b1111, // At SW instruction, writes to the whole
                            default: 'x,
                        };
                    }
                    State::WaitReady: if membus.ready {
                        state = State::WaitValid;
                    }
                    State::WaitValid: if membus.rvalid {
                        state = State::Init;
                    }
                    default: {}
                }
            }
        }
    }

}
